dnl YAZ Toolkit, Index Data 1994-2002
dnl See the file LICENSE for details.
dnl $Id: configure.in,v 1.82 2002-08-29 16:18:34 mike Exp $
AC_INIT(include/yaz/yaz-version.h)
AM_INIT_AUTOMAKE(yaz, 1.8.9)
dnl
AC_SUBST(READLINE_LIBS)
AC_SUBST(YAZ_CONF_CFLAGS)
dnl ------ Checking programs
AC_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL
AM_DISABLE_SHARED
AM_PROG_LIBTOOL
dnl
dnl ----- DOCBOOK DTD
AC_SUBST(DTD_DIR)
AC_ARG_WITH(dtd, [  --with-dtd[=DIR]        Use docbookx.dtd in DIR],
[
   if test -f "$withval/docbookx.dtd"; then
      DTD_DIR=$withval
   fi
],[
   AC_MSG_CHECKING(for docbookx.dtd)
   for d in /usr/share/sgml/docbook/dtd/xml/4.1.2 \
         /usr/share/sgml/docbook/xml-dtd-4.1.2 \
         /usr/share/sgml/docbook/xml-dtd-4.1 \
         /usr/share/sgml/docbook/dtd/xml/4.0 \
	    /usr/lib/sgml/dtd/docbook-xml 
   do
     if test -f $d/docbookx.dtd; then
       AC_MSG_RESULT($d)
       DTD_DIR=$d
       break
     fi
   done
   if test -z "$DTD_DIR"; then
      AC_MSG_RESULT(Not found)
   fi
])
AC_SUBST(DSSSL_DIR)
AC_ARG_WITH(dsssl,[  --with-dsssl[=DIR]      Use DSSSL in DIR/{html,print}/docbook.dsl],
[
   if test -f "$withval/html/docbook.dsl"; then
      DSSSL_DIR=$withval
   fi
],[
   AC_MSG_CHECKING(for docbook.dsl)
   for d in /usr/share/sgml/docbook/stylesheet/dsssl/modular \
            /usr/share/sgml/docbook/dsssl-stylesheets-1.64 \
            /usr/share/sgml/docbook/dsssl-stylesheets-1.59 \
            /usr/lib/sgml/stylesheet/dsssl/docbook/nwalsh 
   do
     if test -f $d/html/docbook.dsl; then
       AC_MSG_RESULT($d)
       DSSSL_DIR=$d
       break
     fi
   done
   if test -z "$DSSSL_DIR"; then
      AC_MSG_RESULT(Not found)
   fi
])
dnl 
dnl ----- Sockets
checkBoth=0
AC_CHECK_FUNC(connect)
if test "$ac_cv_func_connect" = "no"; then
	AC_CHECK_LIB(socket, main, LIBS="$LIBS -lsocket", checkBoth=1)
fi
if test "$checkBoth" = "1"; then
	oldLibs=$LIBS
	LIBS="$LIBS -lsocket -lnsl"
	AC_CHECK_FUNC(accept, , [LIBS=$oldLibs])
fi
AC_CHECK_FUNC(gethostbyname, , AC_CHECK_LIB(nsl, main, [LIBS="$LIBS -lnsl"]))
dnl
dnl ------ Open SSL
openssl=no
AC_ARG_WITH(openssl, [  --with-openssl[=DIR]    OpenSSL library in DIR], [openssl=$withval])
AC_SUBST(SSL_CFLAGS)
AC_SUBST(SSL_LIBS)
AC_SUBST(SSL_DEFS)
USE_SSL=0
SSL_CFLAGS=""
SSL_LIBS=""
SSL_DEFS=""
if test "$openssl" != "no"; then
	xLIBS="$LIBS";
	xCFLAGS="$CFLAGS";
	if test "$openssl" != "yes"; then
                SSL_CFLAGS="-I$openssl/include -I$openssl/include/openssl"
                SSL_LIBS="-L$openssl/lib"
		CFLAGS="$SSL_CFLAGS $CFLAGS"
		LIBS="$SSL_LIBS $LIBS"
	fi
	AC_CHECK_LIB(crypto, main,[SSL_LIBS="$SSL_LIBS -lcrypto"])
        LIBS="$LIBS $SSL_LIBS"
	AC_CHECK_LIB(ssl, SSL_new)
	if test "$ac_cv_lib_ssl_SSL_new" = "yes"; then
                SSL_LIBS="$SSL_LIBS -lssl"
		AC_CHECK_HEADER(openssl/ssl.h,[SSL_DEFS="-DHAVE_OPENSSL_SSL_H=1"; USE_SSL=1])
        else
                SSL_LIBS=""
	fi
	LIBS="$xLIBS"
	CFLAGS="$xCFLAGS"
fi
AM_CONDITIONAL(ISSSL, test $USE_SSL = "1")
dnl
dnl ------ GNU Readline
READLINE_SHARED_LIBADD=""
AC_CHECK_LIB(ncurses, tgetent, [READLINE_SHARED_LIBADD="-lncurses"],
	AC_CHECK_LIB(termcap, tgetent, [READLINE_SHARED_LIBADD="-ltermcap"])
)
READLINE_LIBS=""
AC_CHECK_LIB(readline, readline, [READLINE_LIBS="$READLINE_LIBS -lreadline $READLINE_SHARED_LIBADD"],,$READLINE_SHARED_LIBADD)
AC_CHECK_LIB(history, add_history, [READLINE_LIBS="$READLINE_LIBS -lhistory"])
if test "$ac_cv_lib_readline_readline" = "yes"; then
	AC_CHECK_HEADERS(readline/readline.h readline/history.h)
        xLIBS=$LIBS
        LIBS="$LIBS $READLINE_LIBS"
        AC_TRY_LINK([
	#include <stdio.h>
	#include <readline/readline.h>
	],[
        static void f()
        {
                rl_attempted_completion_over = 0;
        }
        ],AC_DEFINE(HAVE_READLINE_COMPLETION_OVER))
        AC_TRY_LINK([
	#include <stdio.h>
	#include <readline/readline.h>
	],[
        static void f()
        {
                rl_completion_matches (0, 0);
        }
        ],AC_DEFINE(HAVE_READLINE_RL_COMPLETION_MATCHES))
        LIBS=$xLIBS
fi
dnl ------ various functions
AC_CHECK_FUNCS(vsnprintf gettimeofday poll)
if test "$ac_cv_func_poll" = "yes"; then
        AC_CHECK_HEADERS(sys/poll.h)
fi
dnl
dnl ------ tcpd
AC_ARG_ENABLE(tcpd,[  --enable-tcpd[=PREFIX]  enable TCP wrapper for server if available])
if test "$enable_tcpd" != ""; then
	oldLibs=$LIBS
	oldCPPFLAGS=$CPPFLAGS
	if test "$enable_tcpd" != "yes"; then
		LIBS="$LIBS -L$enable_tcpd/lib"
		CPPFLAGS="$CPPFLAGS -I$enable_tcpd/include"
	fi
	AC_MSG_CHECKING(for working tcpd.h)
	LIBS="$LIBS -lwrap -lnsl"
	AC_TRY_LINK([#include <syslog.h>
#include <tcpd.h>
	int allow_severity = LOG_INFO;
	int deny_severity = LOG_WARNING;],
	[struct request_info request_info; int i;
	i = hosts_access(&request_info);],
	tcpd_ok=1, tcpd_ok=0)
	if test "$tcpd_ok" = "0"; then
		AC_MSG_RESULT(no)
		LIBS=$oldLibs
		CPPFLAGS=$oldCPPFLAGS
	else
		AC_MSG_RESULT(yes)
		AC_DEFINE(HAVE_TCPD_H)
	fi
fi
dnl
dnl ------ Headers
AC_CHECK_HEADERS(fnmatch.h iconv.h)
AC_STDC_HEADERS
if test "$ac_cv_header_stdc" = "no"; then
	AC_MSG_WARN(Your system doesn't seem to support ANSI C)
fi
dnl
AC_SUBST(LIBTHREAD)
AC_SUBST(CFLAGSTHREADS)
HAVETHREADS=0
CFLAGSTHREADS=""
LIBTHREAD=""
dnl
dnl ------ GNU threads
AC_ARG_ENABLE(pth, [  --enable-pth            enable GNU threads],[enable_pth=$enableval],[enable_pth=no])
AC_SUBST(LIBPTH)
if test "$enable_pth" = "yes"; then
	OLIBS=$LIBS
	AC_CHECK_LIB(pth,main)
	if test "$ac_cv_lib_pth_main" = "yes"; then
        	AC_CHECK_HEADERS(pth.h)
		if test "$ac_cv_header_pth_h" = "yes"; then
			LIBTHREAD="-lpth"
			CFLAGSTHREADS="-DYAZ_GNU_THREADS=1"
			HAVETHREADS=1
		fi
	fi	
	LIBS=$OLIBS
fi
dnl
dnl ------ POSIX Threads
AC_ARG_ENABLE(threads, [  --disable-threads       disable POSIX threads],[enable_threads=$enableval],[enable_threads=yes])
if test "$enable_threads" = "yes" -a "$HAVETHREADS" = "0"; then
	OLIBS=$LIBS
	OCC=$CC
	AC_CHECK_LIB(pthread,main)
	AC_MSG_CHECKING(for working POSIX Threads)
	AC_TRY_LINK([#include <pthread.h>
	void *func(void *p) { return 0; }
	],[
	pthread_t pthread_id;
	pthread_create (&pthread_id, 0, func, 0);],
		thread_ok=yes,thread_ok=no)
	if test "$thread_ok" = "yes"; then
		AC_MSG_RESULT(yes)
		LIBTHREAD="-lpthread"
		CFLAGSTHREADS="-DYAZ_POSIX_THREADS=1 -D_REENTRANT"
		HAVETHREADS=1
	else
		CC="$CC -pthread"
		AC_TRY_LINK([#include <pthread.h>
			void *func(void *p) { return 0; }
			],[
			pthread_t pthread_id;
			pthread_create (&pthread_id, 0, func, 0);],
			thread_ok=yes,thread_ok=no)
		if test "$thread_ok" = "yes"; then
			AC_MSG_RESULT([yes,BSD])
			CFLAGSTHREADS="-pthread -DYAZ_POSIX_THREADS=1 -D_REENTRANT"
			LIBTHREAD="-pthread"
			HAVETHREADS=1
		fi
	fi
	if test "$thread_ok" = "no"; then
		AC_MSG_RESULT(no)
	fi
	CC=$OCC
	LIBS=$OLIBS
fi
AM_CONDITIONAL(ISTHR, test $HAVETHREADS = "1")
dnl
dnl ------ Memory debugging
AC_ARG_ENABLE(memdebug, [  --enable-memdebug       enable memory debugging],[enable_memdebug=$enableval],[enable_memdebug=none])
if test "$enable_memdebug" = "yes"; then
	AC_DEFINE(TRACE_XMALLOC,2)
elif test "$enable_memdebug" = "no"; then
	AC_DEFINE(TRACE_XMALLOC,0)
fi
dnl
dnl ------ Using this for "in-source" yaz-config
AC_SUBST(YAZ_SRC_ROOT)
AC_SUBST(YAZ_BUILD_ROOT)
YAZ_SRC_ROOT=`cd ${srcdir}; pwd`
YAZ_BUILD_ROOT=`pwd`
dnl
if test -f ${srcdir}/lib/yaz-config.in; then
	rm ${srcdir}/lib/yaz-config.in
fi
sed s%yaz_echo_source=yes%yaz_echo_source=no%g < ${srcdir}/yaz-config.in >${srcdir}/lib/yaz-config.in
dnl
dnl ------ Makefiles
dnl
AC_OUTPUT([
Makefile
yaz.spec
util/Makefile
odr/Makefile
z39.50/Makefile
ill/Makefile
zutil/Makefile
comstack/Makefile
ccl/Makefile
tab/Makefile
retrieval/Makefile
server/Makefile
include/Makefile
include/yaz/Makefile
lib/Makefile
zoom/Makefile
client/Makefile
ztest/Makefile
doc/Makefile
doc/yaz.xml
doc/yazhtml.dsl
doc/yazphp.dsl
doc/yazprint.dsl
yaz-config
lib/yaz-config
],[chmod +x yaz-config lib/yaz-config])
