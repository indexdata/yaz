dnl YAZ Toolkit
dnl (c) Index Data 1994-1998
dnl See the file LICENSE for details.
dnl $Id: configure.in,v 1.1 1998-08-21 14:13:24 adam Exp $
AC_INIT(include/yaz-version.h)
dnl ------ Substitutions
AC_SUBST(ODEFS)
dnl
dnl ------ Checking programs
AC_ARG_ENABLE(gcc, [  --enable-gcc            allow use of gcc if available],
    [gcc_ok=$enableval], [gcc_ok=no])
if test "$gcc_ok" = "yes"; then
    AC_PROG_CC
else
    CC=${CC-cc}
AC_SUBST(CC)
fi
AC_PROG_CPP
AC_PROG_INSTALL
AC_PROG_RANLIB
dnl
dnl ----- Sockets
checkBoth=0
AC_CHECK_FUNC(connect)
if test "$ac_cv_func_connect" = "no"; then
	AC_CHECK_LIB(socket, main, LIBS="$LIBS -lsocket", checkBoth=1)
fi
if test "$checkBoth" = "1"; then
	oldLibs=$LIBS
	LIBS="$LIBS -lsocket -lnsl"
	AC_CHECK_FUNC(accept, , [LIBS=$oldLibs])
fi
AC_CHECK_FUNC(gethostbyname, , AC_CHECK_LIB(nsl, main, [LIBS="$LIBS -lnsl"]))
dnl
dnl ------ Threads
AC_ARG_ENABLE(threads, [  --enable-threads        enable threads if available],
    [threads_ok=$enableval], [threads_ok=no])
if test "$threads_ok" = "yes"; then
	AC_CHECK_HEADERS(pthread.h threads.h)
	AC_CHECK_FUNC(pthread_mutex_lock)
	if test "$ac_cv_func_pthread_mutex_lock" = "no"; then
		AC_CHECK_LIB(pthread, main)
	fi
	ODEFS="$ODEFS -D_REENTRANT"
fi
dnl
dnl ------ Headers
AC_STDC_HEADERS
if test "$ac_cv_header_stdc" = "no"; then
	AC_MSG_WARN(Your system doesn't seem to support ANSI C)
fi
dnl
dnl ------ Makefiles
AC_OUTPUT(Makefile util/Makefile asn/Makefile odr/Makefile comstack/Makefile client/Makefile server/Makefile ztest/Makefile retrieval/Makefile ccl/Makefile makelib/Makefile rfc1006/Makefile)
