dnl YAZ Toolkit
dnl (c) Index Data 1994-2000
dnl See the file LICENSE for details.
dnl $Id: configure.in,v 1.24 2000-03-14 09:06:11 adam Exp $
AC_INIT(include/yaz/yaz-version.h)
AM_INIT_AUTOMAKE(yaz, 1.6)
dnl
dnl ------ Checking programs
AC_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL
AC_PROG_RANLIB
dnl 
dnl ----- yaz-comp: The Yaz Compiler
AC_SUBST(ASNMODULE)
AC_SUBST(ILLMODULE)
AC_SUBST(ILLLIB)
AC_ARG_ENABLE(comp,[  --disable-comp          use old encoders, i.e. disable the YAZ ASN.1 Compiler], , enable_comp=yes)
if test "$enable_comp" = "yes"; then
	ASNMODULE="z39.50"
	ILLMODULE="ill"
	ILLLIB=../ill/libill.a
	ASN_MAKEFILES="z39.50/Makefile ill/Makefile"
	cp -f include/yaz/z-proto.h include/yaz/proto.h
else
	ILLMODULE=""
	ASNMODULE=asn
	ILLLIB=""
	ASN_MAKEFILES="asn/Makefile"
	cp -f include/yaz/prt-proto.h include/yaz/proto.h
fi
dnl
dnl ----- Sockets
checkBoth=0
AC_CHECK_FUNC(connect)
if test "$ac_cv_func_connect" = "no"; then
	AC_CHECK_LIB(socket, main, LIBS="$LIBS -lsocket", checkBoth=1)
fi
if test "$checkBoth" = "1"; then
	oldLibs=$LIBS
	LIBS="$LIBS -lsocket -lnsl"
	AC_CHECK_FUNC(accept, , [LIBS=$oldLibs])
fi
AC_CHECK_FUNC(gethostbyname, , AC_CHECK_LIB(nsl, main, [LIBS="$LIBS -lnsl"]))
dnl
dnl ------ GNU Readline
AC_CHECK_LIB(readline, readline, [LIBS="$LIBS -lreadline"])
if test "$ac_cv_lib_readline_readline" = "no"; then
	AC_CHECK_LIB(readline, readline, [LIBS="$LIBS -lreadline -ltermcap"])
fi
AC_CHECK_LIB(history, add_history, [LIBS="$LIBS -lhistory"])
if test "$ac_cv_lib_readline_readline" = "yes"; then
	AC_CHECK_HEADERS(readline/readline.h readline/history.h)
fi
dnl ------ snprintf
AC_CHECK_FUNCS(vsnprintf)
dnl
dnl ------ tcpd
AC_ARG_ENABLE(tcpd,[  --enable-tcpd           enable TCP wrapper for server if available])
if test "$enable_tcpd" = "yes"; then
	AC_MSG_CHECKING(for working tcpd.h)
	oldLibs=$LIBS
	LIBS="$LIBS -lwrap -lnsl"
	AC_TRY_LINK([#include <syslog.h>
	#include <tcpd.h>
	int allow_severity = LOG_INFO;
	int deny_severity = LOG_WARNING;],
	[struct request_info request_info; int i;
	i = hosts_access(&request_info);],tcpd_ok=1, tcpd_ok=0)
	if test "$tcpd_ok" = "0"; then
		AC_MSG_RESULT(no)
		LIBS=$oldLibs
	else
		AC_MSG_RESULT(yes)
		AC_DEFINE(HAVE_TCPD_H)
	fi
fi
dnl
dnl ------ Headers
AC_STDC_HEADERS
if test "$ac_cv_header_stdc" = "no"; then
	AC_MSG_WARN(Your system doesn't seem to support ANSI C)
fi
dnl
dnl ------ Threads
AC_ARG_ENABLE(threads, [  --disable-threads       disable threads],[enable_threads=$enableval],[enable_threads=yes])
if test "$enable_threads" = "yes"; then
	AC_CHECK_LIB(pthread,main)
	AC_MSG_CHECKING(for working POSIX Threads)
	AC_TRY_LINK([#include <pthread.h>
	int func(void *p) { return 0; }
	],[
	pthread_t pthread_id;
	int r = pthread_create (&pthread_id, 0, func, 0);],
		thread_ok=yes,thread_ok=no)
	if test "$thread_ok" = "yes"; then
		AC_MSG_RESULT(yes)
		AC_DEFINE(HAVE_PTHREAD_H)
		AC_DEFINE(_REENTRANT)
	else
		AC_MSG_RESULT(no)
	fi
fi
dnl
SUBDIRS_VAR="util odr $ASNMODULE $ILLMODULE zutil comstack ccl tab retrieval server include lib client ztest"
AC_SUBST(SUBDIRS_VAR)
dnl
dnl ------ Makefiles
dnl
AC_OUTPUT(Makefile util/Makefile odr/Makefile z39.50/Makefile asn/Makefile ill/Makefile zutil/Makefile comstack/Makefile ccl/Makefile tab/Makefile retrieval/Makefile server/Makefile include/Makefile include/yaz/Makefile lib/Makefile client/Makefile ztest/Makefile doc/Makefile)
